#!/bin/false
# DO NOT EXECUTE!  Invoke by "source" only

if ( ! $?cshrc_sourced ) then
set cshrc_sourced

set history=150
set filec
set notify y
set nobeep

#if ( { tty -s } && ! ${?DT} ) then
#  if (! $?BACKSPACE) then
#    setenv BACKSPACE CTRL_H
#  endif
#  switch ($BACKSPACE)
#    case DEL:
#      stty -F `tty` erase "^?"
#      breaksw
#    case CTRL_H:
#      stty -F `tty` erase 
#      breaksw
#    default:
#      echo "Unknown Backspace setting '$BACKSPACE'; leaving stty unchanged"
#  endsw
#  stty kill ^U
#endif

alias a alias

if ($?tcsh) then
  a setprompt ''
  if (! $?prompt) then
    set prompt="[%!\]\<%m\>[%~]%# "
  endif
else
  set cpu  =     `uname -n | cut -f1 -d.`
  a getp	'dirs | cut -f1 -d\ '
  a setprompt	set prompt='\[\!\]\<$cpu\>\[`getp`\]\> '
endif

a cd    'chdir \!* ;setprompt'
a h     history
a s 	source
#a r	rlogin
if ( $?EMACS ) then
  a m	cat
  set simple_ll
else
  a m	more
endif
if ($?simple_ll) then
  a ll	'ls -laF \!* | m'
  a lf	'ls -lF \!* | m'
  a lw	'ls -F \!* | m'
else
  a ll	'ls -laF \!* | less -EFmQRX'
  a lf	'ls -lF \!* | less -EFmQRX'
  a lw	'ls -F \!* | less -EFmQRX'
endif
a pushd	'pushd \!*;setprompt'
a popd	'popd;setprompt'
a push	pushd .
a pop	popd
a md	mkdir
a rd	rmdir
a c	clear
a z	exit
a s	source
a com	gzip
a uc	gunzip
a zcat	'gunzip -c'
a k	less
a zl	'zcat \!* | k'
a f	'finger \!* | m'
a af	'a | grep \!*'
a ct	'cd /tmp/${USER}'
a qu	'quota -v'
a spy	'ps -guxawww | grep \!*'
a du	'du -k'
a perm	'chmod -R go+rX .'
a lperm	'chgrp -R lrn . ; chmod -R g+rwX,o+rX .'
a from	'formail -X "From: " -X "Subject:" -s < /var/mail/${USER}'
a kgv	'kghostview'
a df	'df -h'
a web	'kfmclient openProfile webbrowsing'
a show	/Applications/Preview.app/Contents/MacOS/Preview
#a ccom	'cvs commit -m ""'
#a cup	'cvs update -d'
#a cadd	'cvs add'
a scom	'svn commit -m ""'
a jcom	'svn commit -F ${HOME}/shared/rsvn/research/etc/unm_paranoia_message.txt --force-log'
a sup	'svn update'
a sadd	'svn add'
a sstat	'svn status'
a smv	'svn mv'
a sigtex	'svn propset -F ~/private/etc/svn_tex_ignore.txt svn:ignore .'
a cnun	'chmod -R go-rwx .'
a matlab 'setenv DISPLAY :0.0; /Applications/MATLAB704/bin/matlab -nojvm -nosplash'
a wipe	'srm -mzv'

# handy setup for the ssh-agent program
a agent 'if ( ! -f ${HOME}/private/run/ssh-agent-info ) then ; \
           ssh-agent -c >&! ${HOME}/private/run/ssh-agent-info ; \
           source ${HOME}/private/run/ssh-agent-info ; \
         else ; \
           source ${HOME}/private/run/ssh-agent-info ; \
         endif'
a kagent 'if ( ${?SSH_AGENT_PID} ) then ; \
            kill -HUP ${SSH_AGENT_PID} ; \
            rm -f ${HOME}/private/run/ssh-agent-info ; \
          else if ( -f ${HOME}/private/run/ssh-agent-info ) then ; \
            eval `grep PID ${HOME}/private/run/ssh-agent-info` ; \
            kill -HUP ${SSH_AGENT_PID} ; \
            rm -f ${HOME}/private/run/ssh-agent-info ; \
          else ; \
            echo "No ssh agent currently running..." ; \
          endif'

# for timestampping filenames
a tstr	'date +"%Y%m%d%H%M%S"'

# write world unreadable files by default...
#if ( $?gid ) then
#  if ( $gid == 1 ) then
#    umask 077
#  else
#    umask 07
#  endif
#else			# can't tell, so assume
#  umask 077
#endif
umask 077

# on Mac OS X, when Java 5 isn't the default:
setenv JBINPATH	/System/Library/Frameworks/JavaVM.framework/Versions/1.5/Commands/
a java ${JBINPATH}/java
a javac ${JBINPATH}/javac

cd .
if ( $?tcsh ) source ${HOME}/.tcshrc

unset cshrc_sourced

endif			# cshrc_sourced
