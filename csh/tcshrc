#!/bin/false
# Do not execute!  Invoke by "source" only.

# temporary hack...
setenv BACKSPACE DEL

if ( ! $?tcshrc_sourced ) then
set tcshrc_sourced

if ( $?autologout) unset autologout

set prompt="[%!\]\<%m\>[%c02]%# "
set ellipsis
set prompt3="IS THIS CORRECT?> %R (yes|no|edit|abort) "
set fignore=(.o)
#set histdup=prev
set listmax=20
set symlinks=expand
#set datadir=/home/mow/d/terran/data/anomaly/purdue/${USER}
#set histfile=${datadir}/history.`date +"%Y%m%d%H%M%S"`.$$
set savehist=50
set tperiod=30
alias periodic	'history -S'

if (! $?DISPLAY && $?REMOTEHOST) then
  setenv DISPLAY ${REMOTEHOST}:0
endif
#if ( $gid != 1 ) then
#  umask 7
#endif

bindkey -b C-I	 	complete-word-fwd
bindkey "	"	complete-word-back
bindkey -b C-D		list-choices

# Get the bloody backspace key handling right, whatever the fuck it happens
# to be on this keyboard
#if (! $?BACKSPACE) then
#  setenv BACKSPACE "CTRL_H"
#endif
#switch ($BACKSPACE)
#  case "DEL":
#    bindkey -b C-?	backward-delete-char
#    bindkey "^[^?"	backward-delete-word
#    breaksw
#  case "CTRL_H":
#    bindkey -b C-?		delete-char
#    bindkey ""		delete-word
#    bindkey -b C-H		backward-delete-char
#    bindkey "^[^H"		backward-delete-word
#    breaksw
#  default:
#    echo "Unknown backspace code '$BACKSPACE'.  Leaving tcsh bindings alone..."
#endsw

complete cd		'n/*/d/'
complete rd		'n/*/d/'
complete complete	'p/1/c/'
complete bindkey	'p/2/b/' 'p/3/b/'
complete dd		'c/if=/f/' 'c/of=/f/'
complete kill		'c/-/S/' 'n/*/`ps | cut -c-5`/'
complete man		'n/*/c/'
complete set		'p/1/s/='
complete unset		'p/1/s/'
complete setenv		'p/1/e/'
complete unsetenv	'p/1/e/'
complete find		'c/-/(name print exec type perm atime xdev user)/' \
				'n/-type/(b c d f l p s)/' 'p/*/d/'
complete cc		'c/-[IL]/d/'
complete gcc		'c/-I/d/' 'c/-L/d/'
complete g++		'c/-I/d/' 'c/-L/d/'
complete limit		'p/1/(coredumpsize cputime filesize stacksize \
				datasize memoryuse)/' \
			'p/2/(0 100 1000 unlimited)/'
complete latex		'n/*/f:*.{tex,ltx}/'
complete pdflatex	'n/*/f:*.{tex,ltx}/'
complete dvips		'n/*/f:*.dvi/'
complete dvipdfm	'n/*/f:*.dvi/'
complete xdvi		'n/*/f:*.{dvi,dvi.gz}/'
complete kdvi		'n/*/f:*.dvi/'
complete ghostview	'n/*/f:*.{ps,eps}/'
complete gv		'n/*/f:*.{ps,eps,ps.Z,eps.Z,ps.gz,eps.gz,pdf,pdf.Z,pdf.gz}/'
complete kgv		'n/*/f:*.{ps,eps,ps.Z,eps.Z,ps.gz,eps.gz,pdf,pdf.Z,pdf.gz}/'
complete gvp		'n/*/f:*.{ps,eps,ps.Z,eps.Z,ps.gz,eps.gz}/'
complete xpdf		'n/*/f:*.pdf/'
complete acroread	'n/*/f:*.pdf/'
complete elm		"c@=@F:${HOME}/private/docs/Mail/@"
complete cvs		'p/1/(add admin checkout commit diff export \
			history import log rdiff release remove rtag \
			status tag update)/'
complete uc		'n/*/f:*.{Z,z,gz,tgz}/'
complete zcat		'n/*/f:*.{Z,z,gz,tgz}/'

alias dirs		dirs -v

# terminal specific bindings

if ( $?TERM ) then
  switch ( $OSTYPE.$TERM )
    case "*.dumb":
      if ( $?EMACS ) then		# shell buffer of emacs
        stty nl -echo -istrip -opost -onlcr
        unset edit
        unset filec
      endif
      breaksw
    case "linux.con*":			# PC compat keyboard codes
      bindkey "[1~"	beginning-of-line
      bindkey "[4~"	end-of-line
      bindkey "[C"	forward-word
      bindkey "[D"	backward-word
      breaksw
    case "linux.xterm":
      bindkey "ÿ"	backward-delete-word
      breaksw
  endsw
endif

source ${HOME}/.cshrc

unset tcshrc_sourced
endif			# tcshrc_sourced

test -r /sw/bin/init.csh && source /sw/bin/init.csh
